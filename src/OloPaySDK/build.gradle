plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "org.jetbrains.dokka"
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        versionName '2.0.0' //THIS MUST BE UPDATED EVERY RELEASE FOR METADATA TO WORK PROPERLY
        minSdk 23
        buildConfigField "String", "SDK_VERSION", "\"${defaultConfig["versionName"]}\""
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    flavorDimensions "OloPayEnv"
    publishNonDefault true
    productFlavors {
        Dev {
            dimension "OloPayEnv"
            buildConfigField "String", "SDK_BUILD_TYPE", "\"Internal\""
        }
        Prod {
            dimension "OloPayEnv"
            buildConfigField "String", "SDK_BUILD_TYPE", "\"Public\""
        }
    }
    namespace 'com.olo.olopay'
}

ext {
    PUBLISH_GROUP_ID = 'com.olo.olopay'
    PUBLISH_VERSION = System.getenv('SDK_PUBLISH_VERSION')
    PUBLISH_ARTIFACT_ID = 'olo-pay-android-sdk'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
apply plugin: 'kotlin-android'

tasks.withType(dokkaHtml.getClass()).configureEach {
    pluginsMapConfiguration.set(
        ["org.jetbrains.dokka.base.DokkaBase": """{ 
            "separateInheritedMembers": true, 
            "customStyleSheets": ["${project.projectDir}/docs/styles/olopay-styles.css"]
        }"""]
    )
}

tasks.withType(dokkaJavadoc.getClass()).configureEach {
    pluginsMapConfiguration.set(
        ["org.jetbrains.dokka.base.DokkaBase": """{ 
            "separateInheritedMembers": true, 
            "customStyleSheets": ["${project.projectDir}/docs/styles/olopay-styles.css"]
        }"""]
    )
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            skipDeprecated.set(true)
            includeNonPublic.set(false)
            suppressObviousFunctions.set(true)
            suppressInheritedMembers.set(true)
            skipEmptyPackages.set(true)
            displayName.set("Olo Pay SDK")
            reportUndocumented.set(true)
            includes.from(["docs/setup.md"])
            samples.from(["docs/Changelog.kt"])
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.9.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    api 'com.stripe:stripe-android:20.25.5'

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation 'org.mockito:mockito-android:4.9.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'
}
repositories {
    mavenCentral()
}